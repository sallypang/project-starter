/**
 * JavaCC file
 */
 
options {
  JDK_VERSION = "1.6";
  STATIC = false;
}
PARSER_BEGIN(JCCExpressionsParser)
package parser.jcc;

import ast.*;
import java.util.ArrayList;
import java.util.List;

public class JCCExpressionsParser {
}

PARSER_END(JCCExpressionsParser)

SKIP :
{
 	" "
|	"\r"
|	"\t"
|	"\n"
|	< MULTI_LINE_COMMENT: "/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/" >
|   < SINGLE_LINE_COMMENT: "//" (~["\n"])* >
}

TOKEN : /* KEYWORDS */
{	
	<PRINT:		"print" >
| 	<INT:		"int" >
| 	<BOOL:		"boolean" >
| 	<RETURN:	"return" >
}
TOKEN : /* SEPARATORS and OPERATORS*/
{	<LPAREN: "(">
|	<RPAREN: ")">
|	<ASSIGN: "=">
|	<QUESTION:	 "?">
|	<COLON:	 ":" >
|	<PLUS:	 "+" >
|	<MINUS:  "-" >
|	<MULT:   "*" >
|	<SMALLER:"<">
|	<NOT:	 "!">
| 	<SEMICOLON:	 ";" >
}
	
TOKEN :
{   < INTEGER_LITERAL: ( <DIGIT> )+ >
|   < IDENTIFIER: <LETTER> (<LETTER> | "_" | <DIGIT>)* >
|   < #LETTER: ["A" - "Z", "a" - "z"] >
|   < #DIGIT: ["0" - "9"] >
}

Program Program() : { 
	NodeList<Statement> ss = new NodeList<Statement>();
	Statement s; 
	Expression e;
}{	( s=Statement()
		{ ss.add(s); } 
	)*
	< PRINT > e = Expression()
	<EOF>
	{ return new Program(ss, new Print(e)); }
}
Statement Statement() : {
	Statement s, f; //TODO: should not be null ever!
}
{	s=Assign()	{ return s; }
|  	f=FunctionDeclaration() { return f; }  	
}
Statement Assign() : {
	String name;
	Expression value;
}
{	name=Identifier() 
	"="
	value=Expression() ";"
		{return new Assign(name, value); }
}


Expression Expression() : {
	Expression e, e2, e3;
}
{	e=CompExpression() 
	( "?" e2 = Expression()
	  ":" e3 = Expression()
		{	e = new Conditional(e, e2, e3); }
	)?
	{ return e; }
}

// For parsing anything with priority same or higher than <
Expression CompExpression() : {
	Expression e, e2;
}
{ 	e=AddExpression() 
	( "<" e2=AddExpression() 
		{ e = new LessThan(e, e2); }
	)?
	{ return e; }
}
// For parsing anything with priority same or higher than +
Expression AddExpression() : {
	Expression e, e2;
	Token op;
}
{	e=MultExpression() 
	( 	(op="+"|op="-") 
		e2=MultExpression() 
			{ if (op.image.equals("+"))
				e=new Plus(e, e2);
			  else
			  	e=new Minus(e, e2);
			}	
	)*
	{ return e; }
}

// For parsing anything with priority same or higher than *
Expression MultExpression() :  {
	Expression e, e2;
}
{	e=NotExpression() 
	(	"*" e2=NotExpression()
			{ e = new Times(e, e2); } 
	)*
	{ return e; }
}

// For parsing anything with priority same or higher than ! expressions:
Expression NotExpression() : {
	Expression e;
}
{ 	"!" e=NotExpression()
		{ return new Not(e); }
| 	e=PrimaryExpression()
		{ return e; }
}
        
/* PrimaryExpression is the expression that has highest precedence.*/
Expression PrimaryExpression() : {
	Token t;
	String i, i2;
	Expression e, e2;
}
{ t=<INTEGER_LITERAL>					{ return new IntegerLiteral(t.image); }
| i=Identifier()						{ return new IdentifierExp(i); }
| "(" e=Expression() ")"				{ return e; }
//| i2=Identifier() "(" ( e=Expression() ( "," e2=Expression() )*  )? ")" {return e;}
}

String Identifier() : {
	Token i;
}
{
	i = <IDENTIFIER>
	{ return i.image; }
}

// Functions
Statement FunctionDeclaration() : {
    Type t;
    String funcName, name;
	Expression ret;
	Statement s;
	List<Param> params = new ArrayList<Param>();
	NodeList<Statement> body = new NodeList<Statement>();
}
{ t=Type() funcName=Identifier()
// Params
"(" 
    ( t=Type() name=Identifier()  {params.add(new Param(t,name));}
    ( "," t=Type() name=Identifier() {params.add(new Param(t,name));})*  )?
")"

// Func. body
"{"    ( s=Assign() {body.add(s);})* "return" ret=Expression() ";"
"}"

	{return new FunctionDeclaration(t, funcName, params, body, ret); }
}

/* Types.*/
Type Type() : {
	Token i, b;
}
{ i="int"					{ return new IntegerType(); }
| b="boolean"				{ return new BooleanType(); }
}